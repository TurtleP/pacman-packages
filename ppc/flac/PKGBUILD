# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>
# Contributor: oreo639 <oreo6391@gmail.com>

pkgname=ppc-flac
pkgver=1.3.4
pkgrel=1
pkgdesc='Open source Free Lossless Audio Codec'
arch=('any')
url='https://xiph.org/flac/'
license=(Xiph.org)
options=(!strip libtool staticlibs)
depends=("ppc-libogg")
makedepends=('dkp-toolchain-vars')
groups=('ppc-portlibs')

source=(
  "https://ftp.osuosl.org/pub/xiph/releases/flac/flac-$pkgver.tar.xz"
  "flac-${pkgver}.patch"
)
sha256sums=(
  '8ff0607e75a322dd7cd6ec48f4f225471404ae2730d0ea945127b1355155e737'
  'd9e0eb8e849a0f8b46a02fdeba50d1573d8e0f3337041296c66de64fe8a85b8d'
)

build() {
  cd flac-$pkgver

  source /opt/devkitpro/ppcvars.sh

  patch -Np1 -i "$srcdir/flac-${pkgver}.patch"

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --disable-examples \
    --disable-xmms-plugin --disable-doxygen-docs \
    --disable-sse --disable-oggtest --disable-examples \
    --disable-thorough-tests

  make -C src/libFLAC
}

package() {
  cd flac-$pkgver

  make DESTDIR="$pkgdir" install
}
